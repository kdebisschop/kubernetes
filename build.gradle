buildscript {
    repositories {
        mavenCentral()
    }
}
plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.11.0'
}

ext.pluginName = 'Kubernetes Rundeck Plugin'
ext.pluginDescription = "Kubernetes Rundeck Plugin to manage pods , deployments, etc"
ext.sopsCopyright = "Â© 2018, Rundeck, Inc."
ext.sopsUrl = "http://rundeck.com"
ext.buildDateString=new Date().format("yyyy-MM-dd'T'HH:mm:ssX")
ext.archivesBaseName = "kubernetes-plugin"
ext.pluginBaseFolder = "."

scmVersion {
    ignoreUncommittedChanges = true
    tag {
        prefix = ''
        versionSeparator = ''
        def origDeserialize=deserialize
        //apend .0 to satisfy semver if the tag version is only X.Y
        deserialize = { config, position, tagName ->
            def orig = origDeserialize(config, position, tagName)
            if (orig.split('\\.').length < 3) {
                orig += ".0"
            }
            orig
        }
    }
}

project.version = scmVersion.version
ext.archiveFilename = ext.archivesBaseName + '-' + version

import org.apache.tools.ant.filters.ReplaceTokens

defaultTasks 'build'

task pluginZip(type: Jar) {
    destinationDir = file("build/libs")
    baseName = project.ext.archivesBaseName
    version = project.version
    classifier = ''
    extension = 'zip'

    from("${project.buildDir}/zip-contents") {
        include("*.yaml")
        include("resources/**")
        include("contents/*")
        into(archiveFilename)
    }

    manifest {
        attributes 'Rundeck-Plugin-Name': pluginName.toString(),
                   'Rundeck-Plugin-Description': pluginDescription.toString(),
                   'Rundeck-Plugin-Archive': 'true',
                   'Rundeck-Plugin-File-Version': version,
                   'Rundeck-Plugin-Author': sopsCopyright,
                   'Rundeck-Plugin-URL': sopsUrl,
                   'Rundeck-Plugin-Date': buildDateString
    }
}

pluginZip.doFirst {
    def assetsMap = new Properties()
    def tokens = assetsMap + [
            version    : version,
            date       : new Date().format("yyyy-MM-dd'T'HH:mm:ssX").toString(),
            author     : sopsCopyright,
            url        : sopsUrl,
            title      : pluginName,
            description: pluginDescription,
            name       : archivesBaseName.toString(),
    ]

    copy {

        from("${project.projectDir}/resources"){
            include '**/*'
            into "resources"
        }

        from("${project.projectDir}/contents"){
            into "contents"
        }

        from("${project.projectDir}/plugin.yaml") {
            filter(ReplaceTokens, tokens: tokens)
            exclude '**/*.png'
        }

        into "${project.buildDir}/zip-contents"
    }
}

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenZip(MavenPublication) {
            artifact pluginZip
        }
    }
}

defaultTasks 'clean', 'build','pluginZip'

task build(dependsOn: ['pluginZip']) { doLast { } }

task install(dependsOn: ['build','publishToMavenLocal']) { doLast { } }

task clean(type: Delete) {
    delete('build')
}
